' Gambas class file

Public Picon As PiconZero
Public Ultrasonic As Ultrasonic

Public Sub Form_Open()
  
  Me.Picon = New PiconZero
  Me.Ultrasonic = New Ultrasonic(20)
  
  DistanceTimer.Start
  ObstacleTimer.Start
  
  TextBoxSpeak.text = Settings["Speak", "rabbit attack imminent"]             'What to say if there is an obstical 10cms or less away from the ltasonic sensor
  TabStrip1.index = Settings["Tab", 0]                                        'Get the last Tab used

End

Public Sub ServoSliders_Change()
  Me.Picon.SetServo(Last.Tag, Last.Value)
End

Public Sub DistanceTimer_Timer()
  Dim Distance As Integer = Me.Ultrasonic.GetDistance()
  LabelDistance.text = Distance & "cm"
  If Distance <= 10 Then
    LabelDistance.Background = Color.Red
    Shell "espeak " & Shell$(TextBoxSpeak.Text)
  Else
    LabelDistance.Background = Color.DarkGreen
  Endif
End

Public Sub ObstacleTimer_Timer()
  ObstacleSensor0.Value = Me.Picon.GetObstacleSensor(0)
  ObstacleSensor1.Value = Me.Picon.GetObstacleSensor(1)
  ObstacleSensor2.Value = Me.Picon.GetObstacleSensor(2)
  ObstacleSensor3.Value = Me.Picon.GetObstacleSensor(3)
End

Public Sub MotorSliders_Change()
  Me.Picon.SetMotor(Last.Tag, Last.Value)
End

Public Sub MotorsStop_Click()
  SliderMotorA.Value = 0
  SliderMotorB.Value = 0
End

Public Sub Form_Resize()
  Dim NewFontSize As Float = (Me.Width - 50) / 5 
  
  If NewFontSize < 5 Then NewFontSize = 5
  LabelDistance.Font.Size = NewFontSize
End
 
Public Sub Form_Close()
  Me.Ultrasonic.Close
  Settings["Speak"] = TextBoxSpeak.text
  Settings["Tab"] = TabStrip1.Index
End
